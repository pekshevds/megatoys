///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
    	
	Ответственный    = Пользователи.ТекущийПользователь();
	Автор            = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ                             
	|	Состав.НомерСтроки КАК НомерСтроки,
	|	&ВидДвижения КАК ВидДвижения,
	|	Состав.Ссылка.Дата КАК Период,
	|	Состав.Номенклатура КАК Номенклатура,
	|	Состав.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Состав.Количество КАК Количество,
	|	Состав.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаХранения,
	|	Состав.ВидСтадииОбработки КАК ВидСтадииОбработки,
	|	Состав.МестоХранения КАК МестоХранения
	|ИЗ
	|	Документ.mega_ПриходнаяНакладная.Состав КАК Состав
	|ГДЕ
	|	Состав.Ссылка = &СсылкаНаОбъект
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("СсылкаНаОбъект", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("ВидДвижения", ВидДвиженияНакопления.Приход);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();                                             
	
    РегистрыНакопления.mega_СкладскиеЗапасы.ВыполнитьДвижение(ЭтотОбъект.Ссылка, Выборка);
    
    Движения.mega_ПотребностьВСырьеИМатериалах.Записывать = Истина; 
       
    Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Состав.Номенклатура КАК Номенклатура,
	|	Состав.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(Состав.Количество) КАК Количество,
	|	Состав.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаХранения
	|ИЗ
	|	Документ.mega_ПриходнаяНакладная.Состав КАК Состав
	|ГДЕ
	|	Состав.Ссылка = &СсылкаНаОбъект
	|СГРУППИРОВАТЬ ПО
	|	Состав.Номенклатура,
	|	Состав.ЕдиницаИзмерения,
	|	Состав.Номенклатура.ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПотребностьВСырьеИМатериалахОстатки.Номенклатура КАК Номенклатура,
	|	ПотребностьВСырьеИМатериалахОстатки.ДокументПотребности КАК ДокументПотребности,
	|	ПотребностьВСырьеИМатериалахОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.mega_ПотребностьВСырьеИМатериалах.Остатки(, Номенклатура В
	|		(ВЫБРАТЬ
	|			mega_ПриходнаяНакладнаяСостав.Номенклатура
	|		ИЗ
	|			Документ.mega_ПриходнаяНакладная.Состав КАК mega_ПриходнаяНакладнаяСостав
	|		ГДЕ
	|			mega_ПриходнаяНакладнаяСостав.Ссылка = &СсылкаНаОбъект)) КАК ПотребностьВСырьеИМатериалахОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПотребностьВСырьеИМатериалахОстатки.ДокументПотребности.Дата
	|АВТОУПОРЯДОЧИВАНИЕ
	|ИТОГИ
	|	СУММА(Количество)
	|ПО
	|	Номенклатура,
	|	ДокументПотребности";
	
	Запрос.УстановитьПараметр("СсылкаНаОбъект", ЭтотОбъект.Ссылка);
	
	
	Результаты = Запрос.ВыполнитьПакет();
	ВыборкаСостав = Результаты[0].Выбрать();
	ВыборкаОстатки = Результаты[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСостав.Следующий() Цикл
		
		КоличествоСписать = Справочники.mega_ЕдиницыИзмерения.ПеревестиИзЕдиницыВЕдиницу(
			ВыборкаСостав.Количество, ВыборкаСостав.ЕдиницаИзмерения, ВыборкаСостав.ЕдиницаХранения);
						
		ВыборкаОстатки.Сбросить();
		СтруктураПоиска = Новый Структура("Номенклатура", ВыборкаСостав.Номенклатура);
		Если ВыборкаОстатки.НайтиСледующий(СтруктураПоиска) Тогда
			ВыборкаДокументы = ВыборкаОстатки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДокументы.Следующий() И КоличествоСписать > 0 Цикл
				
				Количество = КоличествоСписать;								
				Если КоличествоСписать > ВыборкаДокументы.Количество Тогда
					
					Количество = ВыборкаДокументы.Количество;
					КоличествоСписать = КоличествоСписать - ВыборкаДокументы.Количество;
				Иначе
					  				
					 КоличествоСписать = 0;
				КонецЕсли;
				
				НоваяСтрока = Движения.mega_ПотребностьВСырьеИМатериалах.Добавить();
				НоваяСтрока.ВидДвижения = ВидДвиженияНакопления.Расход;
				НоваяСтрока.Период = ЭтотОбъект.Дата;
				НоваяСтрока.Регистратор = ЭтотОбъект.Ссылка;
				НоваяСтрока.ДокументПотребности = ВыборкаДокументы.ДокументПотребности;
				НоваяСтрока.Номенклатура = ВыборкаДокументы.Номенклатура;
				НоваяСтрока.Количество = Количество;				
			КонецЦикла;
		КонецЕсли;	                                             	
	КонецЦикла;                                             
	
	
	РегистрыСведений.mega_ЦеныПлановойСебестоимости.УдалитьДвижения(ЭтотОбъект.Ссылка);
	Если ЭтотОбъект.РегистрироватьЦены Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	СоставЦены.Ссылка КАК Регистратор,
			|	СоставЦены.Ссылка.Дата КАК Период,
			|	СоставЦены.Номенклатура,
			|	СоставЦены.ВидСтадииОбработки,
			|	СоставЦены.Цена,
			|	СоставЦены.НомерСтроки КАК НомерСтроки
			|ИЗ
			|	Документ.mega_ПриходнаяНакладная.Состав КАК СоставЦены
			|ГДЕ
			|	СоставЦены.Ссылка = &СсылкаНаОбъект
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки";
		
		Запрос.УстановитьПараметр("СсылкаНаОбъект", ЭтотОбъект.Ссылка);
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		РегистрыСведений.mega_ЦеныПлановойСебестоимости.ВыполнитьДвижение(ЭтотОбъект.Ссылка, ВыборкаДетальныеЗаписи);
	КонецЕсли;	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	ЭтотОбъект.СуммаДокумента = ЭтотОбъект.Состав.Итог("Сумма");
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
		
	Ответственный    = Пользователи.ТекущийПользователь();
	Автор            = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
    
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли