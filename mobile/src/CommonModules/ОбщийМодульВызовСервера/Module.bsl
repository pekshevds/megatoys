
#Область ПрограммныйИнтерфейс

Функция ПроверитьСвязь()Экспорт
	
	ПараметрыСоединения = ПолучитьПараметрыСоединения();
	
	HTTPСоединение = Новый HTTPСоединение(ПараметрыСоединения.Сервер, 
		ПараметрыСоединения.Порт, ПараметрыСоединения.Пользователь, ПараметрыСоединения.Пароль);
		
	HTTPЗапрос = Новый HTTPЗапрос(ПараметрыСоединения.Путь + "/test");
	
	Результат = HTTPСоединение.ВызватьHTTPМетод("GET", HTTPЗапрос);
	Если Результат.КодСостояния >= 200 И Результат.КодСостояния < 400 Тогда
		
		Возврат Истина;		
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

Функция ОбновитьСпецификации()Экспорт
	
	ПараметрыСоединения = ПолучитьПараметрыСоединения();
	
	HTTPСоединение = Новый HTTPСоединение(ПараметрыСоединения.Сервер, 
		ПараметрыСоединения.Порт, ПараметрыСоединения.Пользователь, ПараметрыСоединения.Пароль);
		
	HTTPЗапрос = Новый HTTPЗапрос(ПараметрыСоединения.Путь + "/specifications");
	
	Результат = HTTPСоединение.ВызватьHTTPМетод("GET", HTTPЗапрос);
	Если Результат.КодСостояния >= 200 И Результат.КодСостояния < 400 Тогда
		
		ВходящаяСтрокаJSON = Результат.ПолучитьТелоКакСтроку();
		ВходящаяСтруктура = РазобратьСтрокуJSON(ВходящаяСтрокаJSON);		
				
		Возврат ЗагрузитьСпецификации(ВходящаяСтруктура.data);		
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОчиститьСпецификации()
	
	Попытка
		
		НаборЗаписей = РегистрыСведений.Спецификации.СоздатьНаборЗаписей();
		НаборЗаписей.Записать();				 	
	Исключение
		
		Возврат Ложь;
	КонецПопытки;
		 
	Возврат Истина;
КонецФункции

Функция ЗагрузитьСпецификации(Данные)
	
	 Если ОчиститьСпецификации() Тогда
	 	
	 	БылиОшибки = Ложь;
	 	Для Каждого ТекЭлемент Из Данные Цикл
	 		
	 		Если НЕ РегистрыСведений.Спецификации.ДобавитьЗапись(ТекЭлемент) Тогда
	 			
	 			БылиОшибки = Истина;
	 		КонецЕсли;
	 	КонецЦикла;
	 	
	 	Возврат НЕ БылиОшибки;
	 КонецЕсли;
	 Возврат Ложь;
КонецФункции

Функция РазобратьСтрокуJSON(ВходящаяСтрокаJSON)
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ВходящаяСтрокаJSON);

	Возврат ПрочитатьJSON(ЧтениеJSON, Ложь); 
КонецФункции

Функция ПолучитьПараметрыСоединения()
	
	Структура = Новый Структура();
	Структура.Вставить("Сервер", СокрЛП(Константы.host.Получить()));
	Структура.Вставить("Порт", Константы.port.Получить());
	Структура.Вставить("Пользователь", СокрЛП(Константы.login.Получить()));
	Структура.Вставить("Пароль", СокрЛП(Константы.password.Получить()));
	Структура.Вставить("Путь", СокрЛП(Константы.base.Получить()) + "/hs/exchange/api/v1");
	
	Возврат Структура;
КонецФункции

#КонецОбласти
